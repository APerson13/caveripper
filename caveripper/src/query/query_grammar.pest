WHITESPACE = _{ " " | "\t" }
number = @{ (ASCII_DIGIT)+ }
comparator = { "<" | "=" | ">" }
ident = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }
sublevel_ident = @{ ASCII_ALPHA+ ~ number }
entity = { ident ~ ("/" ~ ident)? }
room_path_component = { ident ~ ("+" ~ entity)* }

// expressions
compare = { entity ~ comparator ~ number }
carry_dist = { entity ~ (^"carry dist" | ^"carry distance" | ^"carry path") ~ comparator ~ number }
straight_dist = { entity ~ (^"straight dist" | ^"straight distance") ~ entity ~ comparator ~ number }
gated = { entity ~ ^"gated" }
not_gated = { entity ~ (^"not gated" | ^"!gated") }
room_path = { room_path_component ~ ("->" ~ room_path_component)* }

// top-level rules
expression = { compare | carry_dist | straight_dist | gated | not_gated | room_path }
query = _{ SOI ~ sublevel_ident ~ expression ~ ("&" ~ sublevel_ident? ~ expression)* ~ EOI }
